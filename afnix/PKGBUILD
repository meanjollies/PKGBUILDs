# Maintainer:  Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: DavidK <david_king@softhome.net>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=afnix
pkgver=2.8.3
pkgrel=1
pkgdesc='Multi-threaded functional programming language'
arch=('x86_64')
url='http://www.afnix.org'
license=('custom')
depends=('ncurses' 'gcc-libs')
source=("${url}/ftp/afnix-src-${pkgver}.tgz")
sha256sums=('a038260b8a2e1bd4eb8cf97510a04f483fa037465eec974ee6a035d46b380f21')

build() {
  cd "${pkgname}-src-${pkgver}"

  ./cnf/bin/afnix-setup -o --prefix="${pkgdir}/usr"
  make
}

package() {
  cd "${pkgname}-src-${pkgver}"

  make install
  install -d "${pkgdir}"/usr/share/emacs/site-lisp
  install etc/*.el -t "${pkgdir}"/usr/share/emacs/site-lisp
  install -D doc/xml/eul/eul.xml "${pkgdir}"/usr/share/licenses/afnix/LICENSE
}
